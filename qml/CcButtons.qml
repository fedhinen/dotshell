import QtQuick
import QtQuick.Layouts
import IslandBackend

RowLayout {
  id: root

  property real buttonBorderWidth
  property string buttonBorderColor
  property real buttonWidth
  property real buttonHeight
  property real buttonRadius
  property color buttonBgOff
  property color buttonFgOff

  property bool controlCenterOpen: false
  property bool mediaAutoOpened: false
  property bool wifiPanelOpened: false
  property bool hasPlayer: false
  property real playerHeight: 0

  anchors.top: parent.top
  anchors.topMargin: hasPlayer ? playerHeight + 92 : 5
  anchors.left: parent.left
  anchors.right: parent.right
  anchors.leftMargin: Theme.contentInset
  anchors.rightMargin: Theme.contentInset

  onControlCenterOpenChanged: {
    if (!controlCenterOpen) root.wifiPanelOpened = false
  }

  // wifi
  Rectangle {
    id: wifiBtn
    width: root.buttonWidth
    height: root.buttonHeight
    radius: root.buttonRadius
    visible: root.controlCenterOpen && !root.mediaAutoOpened
    color: WifiController.enabled ? Theme.oneBg3 : (wifiHover.hovered ? Qt.lighter(root.buttonBgOff, 1.5) : root.buttonBgOff)
    border.width: buttonBorderWidth
    border.color: buttonBorderColor
    scale: wifiMouse.pressed ? 0.93 : 1.0
    clip: true
    Behavior on color { ColorAnimation { duration: 150 } }
    Behavior on scale { NumberAnimation { duration: 80; easing.type: Easing.OutQuad } }

    RowLayout {
      anchors.fill: parent
      anchors.margins: 6
      spacing: 5
      clip: true

      Text {
        text: "\uf1eb" // wifi glyph
        color: WifiController.enabled ? Theme.color12 : root.buttonFgOff
        font { family: Theme.nerdFontFamily; pixelSize: 14 }
      }
      Text {
	
        text: !WifiController.enabled ? "Off"
            : WifiController.currentSsid.length > 0 ? WifiController.currentSsid
            : (WifiController.statusText.length > 0 ? WifiController.statusText : "Not connected")
        color: WifiController.enabled ? Theme.white : root.buttonFgOff
        font { family: Theme.fontFamily; pixelSize: 12; weight: 400 }
        elide: Text.ElideRight
        Layout.fillWidth: true
      }
    }

    HoverHandler { id: wifiHover }
    MouseArea {
      id: wifiMouse
      anchors.fill: parent
      acceptedButtons: Qt.LeftButton | Qt.RightButton
      cursorShape: Qt.PointingHandCursor
      onClicked: (mouse) => {
        if (mouse.button === Qt.RightButton) {
          root.wifiPanelOpened = !root.wifiPanelOpened
          if (root.wifiPanelOpened && WifiController.enabled) WifiController.refreshNetworks(true)
          return
        }
        WifiController.setEnabled(!WifiController.enabled)
      }
    }
  }

  WifiPanel {
    visible: root.wifiPanelOpened
    anchorX: wifiBtn.mapToGlobal(0, 0).x - width  // sit left of the wifi button
    anchorY: wifiBtn.mapToGlobal(0, 0).y
  }

  Item { Layout.fillWidth: true }

  // silent notifications
  Rectangle {
    id: dndBtn
    width: root.buttonWidth
    height: root.buttonHeight
    radius: root.buttonRadius
    visible: root.controlCenterOpen && !root.mediaAutoOpened
    color: notificationModule.dndEnabled ? Theme.color0 : (dndHover.hovered ? Qt.lighter(root.buttonBgOff, 1.5) : root.buttonBgOff)
    border.width: buttonBorderWidth
    border.color: buttonBorderColor
    scale: dndMouse.pressed ? 0.93 : 1.0
    Behavior on color { ColorAnimation { duration: 150 } }
    Behavior on scale { NumberAnimation { duration: 80; easing.type: Easing.OutQuad } }

    Text {
      text: String.fromCodePoint(0xf1f6)
      color: notificationModule.dndEnabled ? Theme.color15 : root.buttonFgOff
      anchors.centerIn: parent
      font { family: Theme.nerdFontFamily; pixelSize: 14 }
    }
    HoverHandler { id: dndHover }
    MouseArea {
      id: dndMouse
      anchors.fill: parent
      cursorShape: Qt.PointingHandCursor
      onClicked: notificationModule.dndEnabled = !notificationModule.dndEnabled
    }
  }

  Item { Layout.fillWidth: true }

  // color temperature
  Rectangle {
    id: temperatureBtn
    width: root.buttonWidth
    height: root.buttonHeight
    radius: root.buttonRadius
    color: colorTemperature.enabled ? Theme.oneBg3
        : (temperatureHover.hovered ? Qt.lighter(root.buttonBgOff, 1.5) : root.buttonBgOff)
    border.width: buttonBorderWidth
    border.color: buttonBorderColor
    scale: temperatureMouse.pressed ? 0.93 : 1.0
    Behavior on color { ColorAnimation { duration: 150 } }
    Behavior on scale { NumberAnimation { duration: 80; easing.type: Easing.OutQuad } }

    RowLayout {
      anchors.centerIn: parent
      spacing: 5

      Text {
        text: "\uf2c9"
        color: colorTemperature.enabled ? Theme.warning : root.buttonFgOff
        font { family: Theme.nerdFontFamily; pixelSize: 14 }
      }

      Text {
        text: colorTemperature.enabled ? colorTemperature.temperature + "K" : "Off"
        color: colorTemperature.enabled ? Theme.white : root.buttonFgOff
        font { family: Theme.fontFamily; pixelSize: 12; weight: 400 }
      }
    }

    HoverHandler { id: temperatureHover }
    MouseArea {
      id: temperatureMouse
      anchors.fill: parent
      cursorShape: Qt.PointingHandCursor
      onClicked: colorTemperature.toggle()
    }
  }
}
